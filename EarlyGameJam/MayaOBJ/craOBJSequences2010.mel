/*
'OBJ Sequence Import/Export' MEL script

Author: Carlos Rico Adega - carlos.rico.3d@gmail.com
Release Date: May 3, 2013

Copyright (c) 2016 Carlos Rico Adega

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:                         

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,k
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

SCRIPT NAME:

craOBJSequences2010.mel

Main procedure:
craOBJSequences2010;


DESCRIPTION:

- This script helps to import and export OBJ sequences.
- It uses choice nodes to handle the animation of imported sequences.
- Allows to export seleted objects as one sequence (combining objects)
or one sequence per object.

LAUNCH SCRIPT :

craOBJSequences2010;


Version: 1.2.1 TESTED ON:

Windows 7 x64
- Maya 2013 x64

Fedora 14 x64 ( Virtualbox Virtual Machine )
- Maya 2012 x64


CHANGE LOG:

Version: 3.0.0
Date: October 12, 2016
    - Support for importing OBJ Sequences with multiple objects
    - Offset Initial Frame attribute default value changed to 1.
    - Added Maya 2010 version. May work on earlier versions (not tested)
	
Version: 2.0.4
Date: July 18, 2016
    - Added Maya 2010 version. May work on earlier versions (not tested)
    - Offset Initial Frame attribute default value changed to 1.
      
Version: 2.0.3
Date: January 18, 2016
    - Fixed bug: residual nodes/meshes were left behind when exporting 
      multiple meshes as a single sequence. 
        
Version: 2.0.2
Date: December 1, 2015
    - Maya 2016+ support

Version: 2.0.1
Date: March 10, 2015
    - Fixed a bug that caused the script to stop working when importing sequences
      without materials (trying to rename lambert1 read-only node)
        
Version: 2.0.0
Date: March 5, 2015
    - Added attributes to imported sequences (animated, cycle animation and offset initial frame)
    - The tool now supports to assign materials when importing
      (materials with textures only seem to work in Maya 2014+)
    - The animation is now controlled by an expression
    - UI redesign
    
Version: 1.2.1
Date: May 27, 2014
    - Fixed minor Maya 2015 UI glitches
        
Version: 1.2
Date: November 22, 2013
    - Added export options
        
Version: 1.1.1
Date: October 10, 2013
    - Added flushUndo command for clearing RAM (thanks to Lossless22 for the tip)

Version: 1.1.0
Date: May 8, 2013
    - Import/Export are now interruptable processes.311
    - Fixed a bug that caused the script to stop working when a file fails to import.

Version: 1.0.0
Date: May 3, 2013
    - First Release.

*/

global proc craOBJSequences2010()
{
    if(!`pluginInfo -q -l "objExport"`)
        loadPlugin -quiet "objExport";
    
    if (`window -exists craOBJSequences2010Win`)
        deleteUI craOBJSequences2010Win;
    
    if(!`optionVar -exists craOBJCollapseExportOptionsOV`)
		optionVar -iv craOBJCollapseExportOptionsOV 1;
	
	if(!`optionVar -exists craOBJGroupsOV`)
		optionVar -iv craOBJGroupsOV 1;
	
	if(!`optionVar -exists craOBJPointGroupsOV`)
		optionVar -iv craOBJPointGroupsOV 1;
	
	if(!`optionVar -exists craOBJMaterialsOV`)
		optionVar -iv craOBJMaterialsOV 1;
	
	if(!`optionVar -exists craOBJSmoothingOV`)
	    optionVar -iv craOBJSmoothingOV 1;
	
	if(!`optionVar -exists craOBJNormalsOV`)
		optionVar -iv craOBJNormalsOV 1;
	
	if(!`optionVar -exists craOBJsApplyMatsOV`)
		optionVar -iv craOBJsApplyMatsOV 1;
	
	window -t " OBJ Sequences I/O v3" -tlb 1 -s 0 craOBJSequences2010Win;
        frameLayout -mw 2 -mh 2 -lv 0 craImportExportOBJsMainFL;
            columnLayout -adj 1 craImportExportOBJsMainCL;
                frameLayout -mw 2 -mh 2 -lv 0 -l "Import Sequence" -cll 0 craImportExportOBJsFL1;
                    columnLayout -adj 1 craImportExportOBJsCL1;
                        rowLayout -nc 2 -adj 2;
                            checkBox -v 1 -l " One transform" -ann "If checked all imported meshes will be collapsed under one transform. If unchecked, groups will be created to hold the imported meshes (allows to edit individual frames)." craOBJSeqOutputCB;
                            iconTextCheckBox -v `optionVar -q craOBJsApplyMatsOV` 
                                -cc "optionVar -iv craOBJsApplyMatsOV #1"
                                -onc "iconTextCheckBox -e -i \"CUBETEXTUREDOWN.XPM\" -ann \"Assign materials on import ENABLED\" craOBJsApplyMatsITCB;optionVar -iv craOBJsApplyMatsOV 1"
                                -ofc "iconTextCheckBox -e -i \"CUBETEXTUREUP.XPM\" -ann \"Assign materials on import DISABLED\" craOBJsApplyMatsITCB;optionVar -iv craOBJsApplyMatsOV 0"
                                -w 24 -h 24 -ann "Toggle assign materials on import"
                                -i "CUBETEXTUREUP.XPM" 
                                craOBJsApplyMatsITCB;
                        setParent ..;
                        checkBox -v 0 -l "Multiple OBJ Sequences" -ann "If checked an OBJ sequence mesh will be created for each object that the OBJ file contain.\n\nFor instance an OBJ Sequence of a tree with trunk, branches and leafs will create\n3 OBJ Sequences if the objects inside de OBJ files are separated" craOBJSeqMultipleCB;
                        if(`optionVar -q craOBJsApplyMatsOV`)
                            iconTextCheckBox -e -i "CUBETEXTUREDOWN.XPM" -ann "Assign materials on import ENABLED" craOBJsApplyMatsITCB;
                        else
                            iconTextCheckBox -e -i "CUBETEXTUREUP.XPM" -ann "Assign materials on import DISABLED" craOBJsApplyMatsITCB;

                        separator -h 2 -st "none";
                        nodeIconButton -style "textOnly" -h 22 -bgc .5 .7 .5 -l "Import OBJ Sequence" -c ("craImportOBJs2010") -ann "Select OBJ Sequence files to import" craOBJSequenceImportNIB;
                    setParent ..;
                setParent ..;
            separator -h 2 -st "none";
                
        frameLayout -mw 2 -mh 2 -lv 0 -l "Export Sequence" -cll 0 craImportExportOBJsFL2;
            columnLayout -adj 1 craImportExportOBJsCL2;
                rowLayout -nc 4 -cw 1 34 -cw 2 32 -cw 3 28 -cw 4 40 -adj 4;
                    nodeIconButton -bgc .7 .7 .7 -w 33 -style "textOnly" -h 20 -c "intField -e -v `currentTime -q` craOBJsStartFrameIF" -l "start" -ann "Click to set start frame to current frame";
                    intField -w 32 -ann "Start frame" -v `playbackOptions -q -min` craOBJsStartFrameIF;
                    nodeIconButton -bgc .7 .7 .7 -w 27 -style "textOnly" -h 20 -l "end" -c "intField -e -v `currentTime -q` craOBJsEndFrameIF" -ann "Click to set end frame to current frame";
                    intField -w 25 -ann "End frame" -v `playbackOptions -q -max` craOBJsEndFrameIF;
                setParent ..;
                
                separator -h 3 -st "none";
                frameLayout -cll 1 -mw 12 -mh 4
                    -ec "optionVar -iv craOBJCollapseExportOptionsOV 0;window -e -h 310 craOBJSequences2010Win" 
                    -cc "optionVar -iv craOBJCollapseExportOptionsOV 1;window -e -h 222 craOBJSequences2010Win" 
                    -cl `optionVar -q craOBJCollapseExportOptionsOV` 
                    -l "Export Options";
                    
                    columnLayout -adj 1;
                        checkBox -w 75 -l "  Groups" -v `optionVar -q craOBJGroupsOV`
                            -cc "optionVar -iv craOBJGroupsOV #1"; 
                        checkBox -l "  Point Groups" -v `optionVar -q craOBJPointGroupsOV` 
                            -cc "optionVar -iv craOBJPointGroupsOV #1";
                        checkBox -w 75 -l "  Materials" -v `optionVar -q craOBJMaterialsOV` 
                            -cc "optionVar -iv craOBJMaterialsOV #1";
                        checkBox -l "  Smoothing" -v `optionVar -q craOBJSmoothingOV` 
                            -cc "optionVar -iv craOBJSmoothingOV #1";
                        checkBox -w 75 -l "  Normals" -v `optionVar -q craOBJNormalsOV` 
                            -cc "optionVar -iv craOBJNormalsOV #1";
                    setParent ..;
                setParent ..;
                
                separator -h 3 -st "none";
                    nodeIconButton -style "textOnly" -h 22 -bgc .7 .5 .2 -l "Export OBJ Sequence" -c "craExportOBJs2010";
                setParent ..; 
            setParent ..;
            separator -h 2 -st "none";
            frameLayout -mw 2 -mh 2 -lv 0 -l "Sequences Toolbar" -cll 0;
                rowLayout -nc 6 -cw 1 24 -cw 2 24 -cw 3 24 -cw 4 24 -cw 6 24 -adj 5;
                    nodeIconButton -w 24 -h 24 -ann "Toggle cycle animation on selected" -style "iconOnly" -i "POSTINFINITYCYCLE.xpm" -c "if(size(`ls -sl`))\n{\n    for($seq in `ls -sl`)\n        setAttr ($seq + \".cycleAnimation\") (`getAttr ($seq + \".cycleAnimation\")` ? 0 : 1);\n}";;
                    nodeIconButton -w 24 -h 24 -ann "Assign default material" -style "iconOnly" -i "PICKPOINTCOMP.xpm" -c "hyperShade -assign lambert1";;
                    nodeIconButton -w 24 -h 24 -ann "Delete Unused Materials" -style "iconOnly" -i "SELECTTEMPLATES.xpm" -c "hyperShadePanelMenuCommand(\"hyperShadePanel1\", \"deleteUnusedNodes\");";;
                    nodeIconButton -w 24 -h 24 -ann "Delete OBJ Sequences" -style "iconOnly" -i "REMOVERENDERABLE.xpm" -c "if(size(`ls -o \"*.isOBJSequence\"`)) delete `ls -o \"*.isOBJSequence\"`;";;
                    text -w 5 -l "" -ann "Carlos Rico Adega © 2016";
                    nodeIconButton -ann "About this Tool" -c "craAboutOBJSequences2010" -i "PICKOTHEROBJ.xpm" -w 24 -h 24;
                setParent ..;
            setParent ..;
        setParent ..;
    setParent ..;
    
    if(!`optionVar -q craOBJCollapseExportOptionsOV`)
        window -e -wh 155 310 -s 0 craOBJSequences2010Win;
    else
        window -e -wh 155 222 -s 0 craOBJSequences2010Win;

    showWindow craOBJSequences2010Win;

    setFocus craOBJSequenceImportNIB;
}

global proc craExportOBJs2010Callback( string $output , string $type )
{
	craExportOBJs2010Cmd $output;
}

global proc craExportOBJs2010()
{
    fileBrowserDialog -mode 4 -an "Set output path" -fc "craExportOBJs2010Callback";
}

global proc craExportOBJs2010Cmd( string $OBJDir )
{
        string $mySel[] = `ls -sl`;
        if(!size($mySel))
            error "Nothing is selected. Select object(s) to export.";
        
		int $multiple = 0;

        int $craCancelled;

        string $craName;
        string $craResult = `promptDialog
            -title "OBJ Sequence"
            -message "Enter sequence name:"
            -button "OK" -button "Cancel"
            -defaultButton "OK" -cancelButton "Cancel"
            -dismissString "Cancel"`
            ;
         
        
        if ($craResult == "OK")
        {
			string $craOBJExportOptions;
			$craOBJExportOptions += "groups=" + string(`optionVar -q craOBJGroupsOV`) + ";";
			$craOBJExportOptions += "ptgroups=" + string(`optionVar -q craOBJPointGroupsOV`) + ";";
			$craOBJExportOptions += "materials=" + string(`optionVar -q craOBJMaterialsOV`) + ";";
			$craOBJExportOptions += "smoothing=" + string(`optionVar -q craOBJSmoothingOV`) + ";";
			$craOBJExportOptions += "normals=" + string(`optionVar -q craOBJNormalsOV`);
			if(size($mySel) > 1)
			{
				string $answer = `confirmDialog -title "Question" -message "                     Multiple objects selected. What do you want to do?"
					-button " One OBJ sequence for all objects " -button " One OBJ sequence per object " -button "Cancel" -defaultButton "One OBJ sequence for all objects"
					-cancelButton "Cancel" -dismissString "Cancel"`;
				if($answer == " One OBJ sequence per object ")
					$multiple = 1;
				else if($answer == "Cancel")
					return;
			}
			
			$craName = `promptDialog -query -text`;
			
			//string $OBJDir = dirname(`fileDialog -m 0 -dm "*." -title "Select a file to set the output folder"`);
			string $exportDir[];

			$exportDir[0] = $OBJDir;

			$startTime = `timerX`;
			
			if(size($exportDir))
			{
                int $start = `intField -q -v craOBJsStartFrameIF`;
                int $end = `intField -q -v craOBJsEndFrameIF`;
                
                int $maxValue = $end - $start;
                progressWindow  -status "Exporting OBJ Sequence..." -maxValue $maxValue -title "Exporting" -isInterruptable true;
                
                currentTime -e $start;
                dgeval $mySel[0];
                
                for( $crai=$start; $crai<=$end; $crai++ )
                {
                    if ( `progressWindow -query -isCancelled` )
                    {
                        $craCancelled = 1;
                        break;
                    }
                    
                    progressWindow -e -step 1 ;
                    
                    if($multiple)
                    {
                        for($j in $mySel)
                        {
                            select -r $j;
                            dgeval $j;
                            string $newDup[] = `duplicate`;
                            craOBJsChannelControlProc2010 {"tx", "ty","tz","rx","ry","rz","sx","sy","sz","v"} 0 1;
                            catchQuiet(`parent -w`);
                            sysFile -md ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $j);
                            file -force -options $craOBJExportOptions -typ "OBJexport" -pr -es ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $j + "/" + $j + "." + craNumberPadding2010 (string($crai), 4, "0") + ".obj");
                            catchQuiet(`delete $newDup`);
                        }
                    }
                    else
                    {
                        select -r $mySel;
                        string $newDup[] = `duplicate`;
                        string $newCombined[] = {};
                        if(size($mySel) > 1)
                            $newCombined = `polyUnite -ch 0`;
                        else
                            catchQuiet(`parent -w`);
                        craOBJsChannelControlProc2010 {"tx", "ty","tz","rx","ry","rz","sx","sy","sz","v"} 0 1;
                        sysFile -md ($exportDir[0] + "/" + $craName + "_OBJ_Seq");
                        file -force -options $craOBJExportOptions -typ "OBJexport" -pr -es ($exportDir[0] + "/" + $craName + "_OBJ_Seq/" + $craName + "." + craNumberPadding2010 (string($crai), 4, "0") + ".obj");
                        catchQuiet(`delete $newDup`);
                        catchQuiet(`delete $newCombined`);
                    }
                    flushUndo;
                    currentTime -e ($crai+1);
                }
			}
			
			select -r $mySel;
			
			progressWindow -endProgress;
			
			$totalTime = `timerX -startTime $startTime`;
			
			if($craCancelled)
				warning "Exporting OBJ sequence cancelled.\n";
			else
				print ("All done! " + $craName + " exported successfully in " + $totalTime + " seconds.\n");
        }
}

global proc craImportOBJs2010()
{
    int $mode = `checkBox -q -v craOBJSeqOutputCB`;
    
     string $OBJDir = dirname(`fileDialog -m 0 -dm "*.obj" -title "Select a file to set sequence folder"`);
     string $OBJfiles[] = `getFileList -fld ($OBJDir + "/") -fs "*.obj"`;
     
     for( $i=0; $i<size($OBJfiles) ;$i++ )
         $OBJfiles[$i] =($OBJDir + "/" + $OBJfiles[$i]); 
    
     if(size($OBJfiles))
    {
        if(`checkBox -q-v craOBJSeqMultipleCB`)
            craImportOBJsMultipleCmd2010($OBJfiles, $mode);
        else
            craImportOBJsCmd2010($OBJfiles, $mode);
    }
    else
            print "No files selected.\n";
}

global proc craImportOBJsMultipleCmd2010( string $OBJfiles[], int $mode )
{
    $startTime = `timerX`;
    string $craErrors[];
    int $craCancelled;
    
    // store the status of "removeDuplicateShadingNetworksOnImport" optionVar
    int $rdsnoiStatus = `optionVar -q "removeDuplicateShadingNetworksOnImport"`;
    // set to remove duplicated shading networks
    optionVar -iv "removeDuplicateShadingNetworksOnImport" 1;
    
    string $origMeshNodes[] = `ls -l -type "mesh"`;
    string $craOrigObjectSetNodes[] = `ls -type "objectSet"`;
    string $currentMeshNodes[] = `ls -l -type "mesh"`;
    
    if(!$mode)
    {
        if(!`objExists "OBJ_Sequences_grp"`)
        {
            group -em -n "OBJ_Sequences_grp";
            addAttr -ln ("isOBJSequence") -at bool -dv 0 OBJ_Sequences_grp;
            setAttr -e -lock true "OBJ_Sequences_grp.isOBJSequence" on;
        }
    }
    
    string $filename = basenameEx("\"" + $OBJfiles[0] + "\"");
    string $groupName[];
    string $OBJshaders[];
    string $OBJshaders_assignments[];
    tokenize $filename "." $groupName;
    tokenize $groupName[0] "_" $groupName;
    
    select -cl;
    int $status = `scriptEditorInfo -q -sr`;
    scriptEditorInfo -sr 1;
    int $count = 1;
    int $maxValue = size($OBJfiles);
    
    string $seqNames[];
    
    progressWindow  -status "Importing Multiple OBJ Sequence..." -maxValue $maxValue -title "Importing" -isInterruptable true;
    
    for($file = 0; $file < size($OBJfiles); $file++)
    {
        if ( `progressWindow -query -isCancelled` )
        {
            $craCancelled = 1;
            break;
        }
        
        if(catchQuiet(`file -force -import -rdn -type "OBJ" -options "mo=1" $OBJfiles[$file]`))
        {
            $craErrors[size($craErrors)] = $OBJfiles[$file];
            continue;
        }
        
        select -r `ls -l -type "mesh"`;
        select -deselect $currentMeshNodes;
        
        if($file == 0)
            $OBJshaders = craOBJSequencesGetShaders2010();
        
        if(!$mode)
            select -r `listRelatives -p`;   
            
        pickWalkUp;
            
        string $newMesh[] = `ls -sl`;
        
        if($file == 0)
            $seqNames = $newMesh;
        
        string $newMeshesNames[];
        
        if(size($newMesh))
        {
            for($cras in $newMesh)
            {
                $newMeshesNames[size($newMeshesNames)] = $groupName[0] + "_" + $cras;
                rename $cras ( $groupName[0] + "_" + $cras + "_" + craNumberPadding2010(string($count), 4, 0) + "_mesh" );
            }
        }
        
        int $nameCount = 0;
        
        if(!$mode)
        {
            string $mySel[] = `ls -sl`;
            
            for($cras in $mySel)
            {
                if(!`objExists ($newMeshesNames[$nameCount] + "_grp")`)
                    group -n ($newMeshesNames[$nameCount] + "_grp") $cras;
                else
                    parent $cras ($newMeshesNames[$nameCount] + "_grp");
                
                $nameCount++;
            }
            select -r $mySel;
            select -r `listRelatives -p`;   
            select -add "OBJ_Sequences_grp";
            catchQuiet(`parent`);
        }
        
        hide;
        
        $currentMeshNodes = `ls -l -type "mesh"`;
        $count++;
        flushUndo;
        progressWindow -e -step 1 ;
    }
    
    select -r `ls -l -type "mesh"`;
    select -deselect $origMeshNodes;
    
    if(!$mode)
    {
        select -r `listRelatives -p`;
        select -r `listRelatives -p`;   
        $newGroup = `group -n ($groupName[0] + "_OBJ_seq")`;
        select -r `listRelatives -f -c $newGroup`;
    }
    
    for($cras in $seqNames)
    {
        select -r ($groupName[0] + "_" + $cras + "_*_mesh");
        craConnectToChoice2010($groupName[0] + "_" + $cras) $mode;        
    }
    
    select -r ($groupName[0] + "_*_Mesh");
    group -n ($groupName[0] + "_OBJ_seq");
    
    // remove imported sets
    string $craCurrentSets[] = `ls -type "objectSet"`;
    string $craRemoveSets[] = `stringArrayRemove $craOrigObjectSetNodes $craCurrentSets`;
    delete $craRemoveSets;
    
    if(size($OBJshaders) > 1)
    {
        string $currentSel[] = `ls -sl`;
        for($shd=0; $shd < size($OBJshaders); $shd+=2)
        {
            if($OBJshaders[$shd] == "lambert1")
                continue;
            catchQuiet(`select -r $OBJshaders[$shd+1]`);
            string $shaderNamed = `rename $OBJshaders[$shd] ($groupName[0] + "_OBJ_Seq_mat")`;
            if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
                hyperShade -assign $shaderNamed;
            select -r $currentSel[0];
        }
    }
    else
    {
        if($OBJshaders[0] != "lambert1")
        {
            string $shaderNamed = `rename $OBJshaders[0] ($groupName[0] + "_OBJ_Seq_mat")`;
            if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
                hyperShade -assign $shaderNamed;
        }
    }
    
    scriptEditorInfo -sr $status;

    $totalTime = `timerX -startTime $startTime`;
    
    if(size($craErrors))
    {
        for($crai in $craErrors)
            print ($crai + " failed to import.\n");
            
        warning ("There were errors importing " + size($craErrors) + " files. See Script Editor for details.\n");
    }
    else
    {
        if($craCancelled)
            warning "Importing OBJ sequence cancelled.\n";
        else
            print ("All done! " + $groupName[0] + " sequence imported successfully in " + $totalTime + " seconds.\n");
    }
    progressWindow -endProgress;
    
    // restore "removeDuplicateShadingNetworksOnImport" optionVar status
    optionVar -iv "removeDuplicateShadingNetworksOnImport" $rdsnoiStatus;
}

global proc craImportOBJsCmd2010( string $OBJfiles[], int $mode )
{
    $startTime = `timerX`;
    string $craErrors[];
    int $craCancelled;
    
    // store the status of "removeDuplicateShadingNetworksOnImport" optionVar
    int $rdsnoiStatus = `optionVar -q "removeDuplicateShadingNetworksOnImport"`;
    // set to remove duplicated shading networks
    optionVar -iv "removeDuplicateShadingNetworksOnImport" 1;
    
    string $origMeshNodes[] = `ls -l -type "mesh"`;
    string $craOrigObjectSetNodes[] = `ls -type "objectSet"`;
    string $currentMeshNodes[] = `ls -l -type "mesh"`;
    
    if(!$mode)
    {
        if(!`objExists "OBJ_Sequences_grp"`)
        {
            group -em -n "OBJ_Sequences_grp";
            addAttr -ln ("isOBJSequence") -at bool -dv 0 OBJ_Sequences_grp;
            setAttr -e -lock true "OBJ_Sequences_grp.isOBJSequence" on;
        }
    }
    
    string $filename = basenameEx("\"" + $OBJfiles[0] + "\"");
    string $groupName[];
    string $OBJshaders[];
    string $OBJshaders_assignments[];
    tokenize $filename "." $groupName;
    tokenize $groupName[0] "_" $groupName;
    
    select -cl;
    int $status = `scriptEditorInfo -q -sr`;
    scriptEditorInfo -sr 1;
    int $count = 1;
    int $maxValue = size($OBJfiles);
    
    progressWindow  -status "Importing OBJ Sequence..." -maxValue $maxValue -title "Importing" -isInterruptable true;
    
    for($file = 0; $file < size($OBJfiles); $file++)
    {
        if ( `progressWindow -query -isCancelled` )
        {
            $craCancelled = 1;
            break;
        }
        
        if(catchQuiet(`file -force -import -rdn -type "OBJ" -options "mo=0" $OBJfiles[$file]`))
        {
            $craErrors[size($craErrors)] = $OBJfiles[$file];
            continue;
        }
        
        select -r `ls -l -type "mesh"`;
        select -deselect $currentMeshNodes;
        
        if($file == 0)
            $OBJshaders = craOBJSequencesGetShaders2010();
        
        if(!$mode)
            pickWalkUp;
            
        string $newMesh[] = `ls -sl`;
        
        if(size($newMesh))
                rename $newMesh[0] ( $groupName[0] + "_" + craNumberPadding2010(string($count), 4, 0) + "_mesh" );
        else
                $craErrors[size($craErrors)] = $OBJfiles[$file];
        
        if(!$mode)
        {
            select -add "OBJ_Sequences_grp";
            parent;
        }
        
        hide;
        
        $currentMeshNodes = `ls -l -type "mesh"`;
        $count++;
        flushUndo;
        progressWindow -e -step 1 ;
    }
    
    select -r `ls -l -type "mesh"`;
    select -deselect $origMeshNodes;
    
    
    if(!$mode)
    {
        pickWalkUp;
        $newGroup = `group -n ($groupName[0] + "_OBJ_seq")`;
        select -r `listRelatives -f -c $newGroup`;
    }
    
    craConnectToChoice2010 $groupName[0] $mode;
    
    // remove imported sets
    string $craCurrentSets[] = `ls -type "objectSet"`;
    string $craRemoveSets[] = `stringArrayRemove $craOrigObjectSetNodes $craCurrentSets`;
    delete $craRemoveSets;
    
    if(size($OBJshaders) > 1)
    {
        string $currentSel[] = `ls -sl`;
        for($shd=0; $shd < size($OBJshaders); $shd+=2)
        {
            if($OBJshaders[$shd] == "lambert1")
                continue;
            select -r $OBJshaders[$shd+1];
            string $shaderNamed = `rename $OBJshaders[$shd] ($groupName[0] + "_OBJ_Seq_mat")`;
            if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
                hyperShade -assign $shaderNamed;
            select -r $currentSel[0];
        }
    }
    else
    {
        if($OBJshaders[0] != "lambert1")
        {
            string $shaderNamed = `rename $OBJshaders[0] ($groupName[0] + "_OBJ_Seq_mat")`;
            if(`iconTextCheckBox -q -v craOBJsApplyMatsITCB`)
                hyperShade -assign $shaderNamed;
        }
    }
    
    scriptEditorInfo -sr $status;

    $totalTime = `timerX -startTime $startTime`;
    
    if(size($craErrors))
    {
        for($crai in $craErrors)
            print ($crai + " failed to import.\n");
            
        warning ("There were errors importing " + size($craErrors) + " files. See Script Editor for details.\n");
    }
    else
    {
        if($craCancelled)
            warning "Importing OBJ sequence cancelled.\n";
        else
            print ("All done! " + $groupName[0] + " sequence imported successfully in " + $totalTime + " seconds.\n");
    }
    progressWindow -endProgress;
    
    optionVar -iv "removeDuplicateShadingNetworksOnImport" $rdsnoiStatus;
}

//craNumberPadding2010 ("20", 4, "0")
global proc string craNumberPadding2010(string $str,int $pad,string $chr)
{
    string $craReturn;
    
    for ($crai=0;$crai<$pad;$crai++)
        if (size($str)<=$crai)
            $craReturn += $chr;
    
    return ($craReturn + $str);
}


// Usage:
//    Select your objects and run. It creates a new choice node, and a new target mesh.
//    Connects every shape node to the choice node. No error checking so select only meshes.

global proc craConnectToChoice2010( string $craName , int $mode)
{
    // get selection
    string $mySel[] = `ls -sl`;
    
    // create choice node with name
    string $choice = `createNode -n ($craName + "_OBJ_Seq_choice") choice`;
    
    // create new mesh to hold the animation
    string $newMesh[] = `polyCube -ch 0 -n ($craName + "_OBJ_Seq_Mesh")`;
    
    // connect choice output to new mesh inMesh attr
    string $meshShape[] = `listRelatives -s $newMesh[0]`;
    connectAttr -f ($choice + ".output") ($meshShape[0] + ".inMesh");
    
    // create attributtes to control the animation
    addAttr -ln ("OBJ_SEQUENCE_OPTIONS") -at char -dv 0 $newMesh[0];
    setAttr -e -keyable true -lock true ($newMesh[0] + ".OBJ_SEQUENCE_OPTIONS");
    
    addAttr -ln ("animated") -at "enum" -en "off:on:" -dv 0 $newMesh[0];
    setAttr -e -keyable true ($newMesh[0] + ".animated") on;
    
    addAttr -ln ("cycleAnimation") -at "enum" -en "on:off:" -dv 0 $newMesh[0];
    setAttr -e -keyable true ($newMesh[0] + ".cycleAnimation") 0;
    
    addAttr -ln ("offsetInitialFrame") -at long -dv 0 $newMesh[0];
    setAttr -e -keyable true ($newMesh[0] + ".offsetInitialFrame");
    setAttr ($newMesh[0] + ".offsetInitialFrame") 1;
    
    addAttr -ln ("isOBJSequence") -at bool -dv 0 $newMesh[0];
    setAttr -e -lock true ($newMesh[0] + ".isOBJSequence") on;

    expression -s ("selector = fmod((frame * " + $newMesh[0] + ".animated) + " + $newMesh[0] + ".offsetInitialFrame*-1, " + size($mySel) + " + (frame * " + $newMesh[0] + ".cycleAnimation))") -o $choice -ae 1 -uc all -n ($choice + "_selector_expr");

    // connect obj outMesh to choice input attr
    for( $crai=0; $crai<size($mySel) ;$crai++ )
    {
        if(!$mode)
        {
            string $shapes[] = `listRelatives -s -f $mySel[$crai]`;
            connectAttr -f ($shapes[0] + ".outMesh") ($choice + ".input[" + ($crai) + "]");
        }
        else
        {
            connectAttr -f ($mySel[$crai] + ".outMesh") ($choice + ".input[" + ($crai) + "]");
            setAttr ($mySel[$crai] + ".intermediateObject") 1;
            setAttr ($mySel[$crai] + ".visibility") 1;
            parent -s -add $mySel[$crai] $newMesh[0];
        }
    }
    
    if($mode)
        delete `listRelatives -f -p $mySel`;
    else 
    {
        select -r `listRelatives -f -p $mySel`;        
        sets -e -forceElement initialShadingGroup $mySel;
    }
    
    select -r $newMesh[0];
}

global proc craAboutOBJSequences2010()
{
    if (`window -exists craAboutOBJSequences2010Win`)
        deleteUI craAboutOBJSequences2010Win;
    
    window -t  " About / Help" -s 0 -tlb 1 craAboutOBJSequences2010Win;
    frameLayout -mw 5 -mh 5 -lv 0;
    frameLayout -mw 5 -mh 5 -lv 0;
    columnLayout -adj 1;
    text -l "OBJ Sequences Import/Export";
    text -l "v3.0.0 for Maya 2010";
    separator -h 5 -st "none";
    frameLayout -mw 5 -mh 5 -lv 0;
        columnLayout -adj 1;
        text -l "HELP";
        text -l "One transform";
        text -l "If checked all imported meshes will be\ncollapsed under one transform. If\nunchecked, groups will be created\nto hold the imported meshes (allows\nto edit individual frames)\n" -al "center";
        rowLayout -nc 3 -adj 3 -cw 1 10 -cw 2 30;
            text -l "";
            iconTextCheckBox -v `optionVar -q craOBJsApplyMatsOV` -w 24 -h 24 -i "CUBETEXTUREUP.xpm" -ann "Assign materials on import toggle";
            text -l "Toggle assign materials on import\nTextures do not work\n";
        setParent ..;
        text -l "Import Sequence";
        text -l "Shows a file dialog for selecting\nthe OBJ sequence files to import\n" -al "center";
        text -l "Export Sequence";
        text -l "If multiple objects are selected a dialog\nshows up to choose whether to make\none OBJ sequence for all objects or\none OBJ sequence for each object\n" -al "center";
        rowLayout -nc 3 -adj 3 -cw 1 10 -cw 2 30;
            text -l "";
            nodeIconButton -w 24 -h 24 -ann "Toggle cycle animation on selected" -style "iconOnly" -i "POSTINFINITYCYCLE.xpm";
            text -l " Toggle cycle animation on selected";
        setParent ..;
        rowLayout -nc 3 -adj 3 -cw 1 10 -cw 2 30;
            text -l "" ;
            nodeIconButton -w 24 -h 24 -ann "Assign default material" -style "iconOnly" -i "PICKPOINTCOMP.xpm";
            text -l " Assign default material to selected";
        setParent ..;
        rowLayout -nc 3 -adj 3 -cw 1 10 -cw 2 30;
            text -l "";
            nodeIconButton -w 24 -h 24 -ann "Delete Unused Materials" -style "iconOnly" -i "SELECTTEMPLATES.xpm";
            text -l " Delete Unused Materials";
        setParent ..;
        rowLayout -nc 3 -adj 3 -cw 1 10 -cw 2 30;
            text -l "";
            nodeIconButton -w 24 -h 24 -ann "Delete OBJ Sequences" -style "iconOnly" -i "REMOVERENDERABLE.xpm" -c "";;
            text -l " Delete OBJ Sequences";
        setParent ..;
        text -l "" -h 1;
    setParent ..;
    setParent ..;
    separator -h 5 -st "none";
    text -l "Carlos Rico Adega © 2016";
    separator -h 5 -st "none";
    textField -ed 0 -text "               carlos.rico.3d@gmail.com";
    separator -h 5 -st "none";
    rowLayout -nc 2 -cw 1 110 -adj 2;
            button -w 110 -l "Downloads" -ann "Free downloads from CreativeCrash.com" -c "showHelp -absolute \"http://www.creativecrash.com/users/charliewales/contributions?tab=free_downloads\"";
            button -w 110 -ann "Linked[In] profile"-l "Linked[In]" -c "showHelp -absolute \"http://www.linkedin.com/pub/carlos-rico/6/58/325\"";
    setParent ..;
    rowLayout -nc 2 -cw 1 110 -adj 2;
            button -w 110 -l "Vimeo" -ann "Vimeo Channel" -c "showHelp -absolute \"https://vimeo.com/channels/749131\"";
            button -w 110 -l "YouTube" -ann "Youtube Channel" -c "showHelp -absolute \"https://www.youtube.com/channel/UCXnSX8PHpQtwJR-uN-atO3Q\"";
    setParent ..;
    setParent ..;
    
    window -e -wh 260 585 -s 0 craAboutOBJSequences2010Win;
    showWindow craAboutOBJSequences2010Win;
}

global proc craOBJsChannelControlProc2010( string $attrs[], int $lock , int $keyable)
{
    string $listedObjects[] = `ls -sl -l`;
    
    for($attr in $attrs)
        for ($object in $listedObjects)
            if(`attributeExists $attr $object`)
                catchQuiet(`setAttr -l $lock -k $keyable ($object + "." + $attr)`);
}


global proc string[] craOBJSequencesGetShaders2010() 
{
    string $origSel[] = `ls -sl`;
    
    string $shaders[];
    string $shaders_assign[];
    // find the shading engine
    string $SG[] = `listConnections -source false -type "shadingEngine"`;
    //find the maya software shader
    for($crai in $SG)
    {
        string $currentShader[] = `listConnections -s 1 ($crai + ".surfaceShader")`;
        //find the mental ray shader
        if(!size($currentShader))
            $currentShader = `listConnections -s 1 ($crai + ".miMaterialShader")`;

        $shaders[size($shaders)] = $currentShader[0];
    }
    //add shader
    if(size($shaders) > 1)
    {
        for($cras in $shaders)
        {
            $shaders_assign[size($shaders_assign)] = $cras;
            hyperShade -objects $cras;

            string $mySel[] = `ls -sl`;
            string $buffer[];
            tokenize $mySel[0] "." $buffer;
            $shaders_assign[size($shaders_assign)] = "." + $buffer[size($buffer)-1];
        }
        select -r $origSel;
        return $shaders_assign;
    }
    else
    {
        select -r $origSel;
        return $shaders;
    }
}